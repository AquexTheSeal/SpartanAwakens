buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "maven-publish"
apply plugin: 'org.spongepowered.mixin'

group = "io.github"
version = "1.16.5-1.1.1"
archivesBaseName = project.modId

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: "official", version: "${project.minecraftVersion}"

//    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=spartan_awakens.mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                spartanawakens {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=spartan_awakens.mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                spartanawakens {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'spartanawakens', '--all', '--output', file('src/main/generated/'), '--existing', file('src/main/resources/')

            arg "-mixin.config=spartan_awakens.mixins.json"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                spartanawakens {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/main/generated'
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraftVersion}-${project.forgeVersion}"

    implementation fg.deobf("io.github:ChaosAwakens:1.16.5-${project.chaosawakens}")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-1.16.5:${project.geckolib}")

    //implementation fg.deobf("com.blackout:chaosadditions:1.16.5-0.3")
    implementation fg.deobf("ignore:chaosadditions:1.16.5-0.4")

    implementation fg.deobf("curse.maven:attributefix-280510:${project.attributefix}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("curse.maven:spartan_weaponry-278141:${project.spartanweaponry}")
    runtimeOnly fg.deobf("curse.maven:spartan_weaponry-278141:${project.spartanweaponry}")

    compileOnly fg.deobf("mezz.jei:jei-${project.minecraftVersion}:${project.jei}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${project.minecraftVersion}:${project.jei}")
    implementation fg.deobf("curse.maven:just-enough-resources-jer-240630:${project.jer}")

    implementation fg.deobf("curse.maven:projecte-226410:${project.projecte}")

    runtimeOnly fg.deobf("curse.maven:mmmm-225738:${project.mmmmmmmmmmmm}")

    runtimeOnly fg.deobf("curse.maven:top-245211:${project.top}")

    runtimeOnly fg.deobf("curse.maven:world_edit-225608:${project.worldedit}")

    runtimeOnly fg.deobf("curse.maven:world_stripper-250603:${project.worldstripper}")
}

mixin {
    // MixinGradle Settings
    add sourceSets.main, 'spartan_awakens.refmap.json'
    config 'spartan_awakens.mixins.json'

    debug.verbose = true
    debug.export = true

    quiet
}

repositories {
    flatDir {
        dirs 'jardependencies'
    }
    maven {
        name = "CurseForge"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "ChaosMaven"
        url = "https://raw.github.com/ChaosAwakens/ChaosMaven/main"
    }
    maven {
        // Geckolib maven location
        name = "GeckoLib"
        url "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "ChaosAdditionsMaven"
        url = "https://raw.github.com/Blackout03/ChaosAdditionsMaven/main"
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.modName,
                "Specification-Vendor"    : "Aqu, Blackout",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.modName,
                "Implementation-Vendor"   : "Aqu, Blackout",
                "Implementation-Version"  : project.modVersion,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')
